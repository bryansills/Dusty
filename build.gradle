buildscript {
    ext.versions = [
        'kotlin': '1.4.21',
        'coroutines': '1.4.2',
        'android_gradle_plugin': '4.0.2',
        'shadow_jar': '6.1.0',
        'ktor': '1.5.0',
        'logback': '1.2.3',
        'appcompat': '1.2.0',
        'constraint_layout': '2.0.4',
        'rxjava': '2.2.20',
        'retrofit': '2.9.0',
        'moshi': '1.11.0',
        'okhttp': '4.9.0'
    ]
    ext.build_config = [
        'compile_sdk': 30,
        'min_sdk': 21,
        'target_sdk': 30
    ]
    ext.deps = [
        'kotlin': [
            'gradle_plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
            'coroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
        ],
        'ktor': [
            'netty': "io.ktor:ktor-server-netty:${versions.ktor}",
            'core': "io.ktor:ktor-server-core:${versions.ktor}"
        ],
        'android': [
            'gradle_plugin': "com.android.tools.build:gradle:${versions.android_gradle_plugin}",
            'appcompat': "androidx.appcompat:appcompat:${versions.appcompat}",
            'constraint_layout': "androidx.constraintlayout:constraintlayout:${versions.constraint_layout}"
        ],
        'shadow_jar': "com.github.jengelman.gradle.plugins:shadow:${versions.shadow_jar}",
        'logback': "ch.qos.logback:logback-classic:${versions.logback}",
        'rxjava': "io.reactivex.rxjava2:rxjava:${versions.rxjava}",
        'retrofit': [
            'core': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
            'rxjava': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
            'moshi': "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
        ],
        'moshi': [
            'kotlin': "com.squareup.moshi:moshi-kotlin:${versions.moshi}",
            'codegen': "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"
        ],
        'okhttp': [
            'logging': "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
        ]
    ]
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath deps.android.gradle_plugin
        classpath deps.kotlin.gradle_plugin
        classpath deps.shadow_jar
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            freeCompilerArgs += [
                    '-XXLanguage:+NewInference',
                    '-progressive',
                    '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi',
                    '-Xuse-experimental=kotlin.time.ExperimentalTime',
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
